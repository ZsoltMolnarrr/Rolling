plugins {
    id 'com.github.johnrengelman.shadow'
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "com.github.breadmoirai.github-release" version "2.4.1"
    id "com.modrinth.minotaur" version "2.+"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven { url 'https://maven.kosmx.dev/' }
    maven { url 'https://maven.shedaniel.me/' }
    maven { url 'https://maven.terraformersmc.com/' }
    maven { url 'https://jitpack.io' }
    maven {
        name = 'Modrinth'
        url = 'https://api.modrinth.com/maven'
        content {
            includeGroup 'maven.modrinth'
        }
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    modImplementation("dev.kosmx.player-anim:player-animation-lib-fabric:${project.player_anim_version}")
    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    shadowBundle(implementation ("com.github.ZsoltMolnarrr:TinyConfig:${project.tiny_config_version}"))

    modImplementation("com.terraformersmc:modmenu:${rootProject.mod_menu_version}") { transitive false }
    modCompileOnly("maven.modrinth:better-combat:${rootProject.better_combat_version}-fabric")
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier.set("dev-shadow")
    relocate 'net.tinyconfig', 'net.combatroll.tinyconfig'
}

remapJar {
    input.set shadowJar.archiveFile
}

if(project.hasProperty('release_type')) {
    tasks.register("publishMod") {
        dependsOn(build)
        finalizedBy(tasks.curseforge)
        finalizedBy(tasks.modrinth)
        finalizedBy(tasks.githubRelease)
    }

    curseforge {
        apiKey = System.getenv("CURSEFORGE_API_KEY")
        project {
            id = '678036'
            changelog = changelog_last_section()
            changelogType = 'markdown'
            releaseType = release_type
            addGameVersion "${rootProject.minecraft_version}"
            addGameVersion "1.20"
            addGameVersion "Fabric"

            relations {
                requiredDependency 'fabric-api'
                requiredDependency 'cloth-config'
                requiredDependency 'playeranimator'
            }

            mainArtifact(remapJar)
            afterEvaluate {
                uploadTask.dependsOn(remapJar)
            }
            options {
                forgeGradleIntegration = false
            }
        }
    }

    modrinth {
        token = System.getenv("MODRINTH_API_KEY")
        projectId = "combat-roll" // This can be the project ID or the slug. Either will work!
        changelog = changelog_last_section()
        versionName = "${version}" // You don't need to set this manually. Will fail if Modrinth has this version already
        versionNumber = "${version}-fabric" // You don't need to set this manually. Will fail if Modrinth has this version already
        versionType = release_type // This is the default -- can also be `beta` or `alpha`
        uploadFile = remapJar // With Loom, this MUST be set to `remapJar` instead of `jar`!
        gameVersions = ["${rootProject.minecraft_version}", "1.20"] // Must be an array, even with only one version
        loaders = ["fabric"] // Must also be an array - no need to specify this if you're using Loom or ForgeGradle
        changelog = changelog_last_section()
        dependencies { // A special DSL for creating dependencies
            // scope.type
            // The scope can be `required`, `optional`, `incompatible`, or `embedded`
            // The type can either be `project` or `version`
            required.project "fabric-api"
            required.project "cloth-config"
            required.project "playeranimator"
        }
    }

    githubRelease {
        token System.getenv("GH_API_KEY") // This is your personal access token with Repo permissions
        owner "ZsoltMolnarrr" // default is the last part of your group. Eg group: "com.github.breadmoirai" => owner: "breadmoirai"
        repo "CombatRoll" // by default this is set to your project name
        tagName "${version}" // by default this is set to "v${project.version}"
        targetCommitish gitBranch() // by default this is set to "main"
        releaseName "Release" // Release title, by default this is the same as the tagName
        generateReleaseNotes false // Generate release notes automatically, if true and body is present, body will be pre-pended, if name is not given, one will be generated by the tag
        // body.set "" // by default this is empty
        draft false // by default this is true
        prerelease release_type != "release" // by default this is false
        // releaseAssets jar.destinationDir.listFiles // this points to which files you want to upload as assets with your release, by default this is empty
        allowUploadToExisting false // Setting this to true will allow this plugin to upload artifacts to a release if it found an existing one. If overwrite is set to true, this option is ignored.
        overwrite false // by default false; if set to true, will delete an existing release with the same tag and name
        // dryRun true // by default false; you can use this to see what actions would be taken without making a release
        apiEndpoint "https://api.github.com" // should only change for github enterprise users
        client // This is the okhttp client used for http requests
    }
}